plugins {
	// swt deps
	id "com.diffplug.gradle.swt.nativedeps" version "3.7.0"
	// osgi
	id "com.diffplug.gradle.osgi.bndmanifest" version "3.7.0"
	// bintray uploading
	id "com.jfrog.bintray" version "1.7.3"
	// github pages
	id "org.ajoberstar.github-pages" version "1.6.0"
	// code formatting
	id "com.diffplug.gradle.spotless" version "3.0.0"
}

repositories {
	mavenCentral()
	// SNAPSHOT versions are free to rely on other SNAPSHOT libraries
	if (project.version.endsWith('-SNAPSHOT')) {
		maven {
			url 'https://oss.sonatype.org/content/repositories/snapshots/'
		}
		configurations.all {
			resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
		}
	}
}

//////////
// JAVA //
//////////
apply plugin: 'java'
sourceSets {
	main { java {
			srcDir 'src'
	} }
	test { java {
			srcDir 'test'
	} }
}
sourceCompatibility = VER_JAVA
targetCompatibility = VER_JAVA

dependencies {
	compile "com.diffplug.durian:durian-core:${VER_DURIAN}"
	compile "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	compile "com.diffplug.durian:durian-concurrent:${VER_DURIAN}"
	compile "com.diffplug.durian:durian-rx:${VER_DURIAN_RX}"
	compile "io.reactivex.rxjava2:rxjava:${VER_RXJAVA}"

	testCompile "junit:junit:${VER_JUNIT}"
	testCompile "com.diffplug.durian:durian-debug:${VER_DURIAN_DEBUG}"
}

//////////
// OSGI //
//////////
jar.manifest.attributes (
	'Export-Package': 'com.diffplug.common.swt.*',
	'Bundle-SymbolicName': 'com.diffplug.durian.swt',
	'Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8',
	'Bundle-Vendor': 'DiffPlug',
	'Bundle-DocURL': "https://github.com/${project.org}/${project.name}",
	'Bundle-License': "https://github.com/${project.org}/${project.name}/blob/v${project.version}/LICENSE",
	'-removeheaders': 'Bnd-LastModified,Bundle-Name,Created-By,Tool',
)
// copy the manifest into the WC
osgiBndManifest {
	copyTo 'META-INF/MANIFEST.MF'
}

/////////////////////////
// INTERACTIVE TESTING //
/////////////////////////

// standard `gradlew test` will autoclose after 500ms
test {
	systemProperty 'com.diffplug.test.autoclose.milliseconds', '500'
	useJUnit {
		// there are some tests that can't pass without a user, so we'll exclude them
		excludeCategories 'com.diffplug.common.swt.InteractiveTest$FailsWithoutUser'
		// SWT tests don't work in gradle on OS X (https://github.com/ReadyTalk/swt-bling/issues/4)
		boolean isMac = System.getProperty("os.name").toLowerCase().contains("mac");
		if (isMac) {
			excludeCategories 'com.diffplug.common.swt.InteractiveTest'
		}
	}
	// log all test events
	testLogging {
		events "failed", "passed", "skipped", "standard_error", "standard_out", "started"
	}
}

// only run the interactive tests
task interactiveTest(type: Test) {
	systemProperty 'com.diffplug.test.autoclose.milliseconds', null
	useJUnit {
		includeCategories 'com.diffplug.common.swt.InteractiveTest'
	}
}

/////////////
// ECLIPSE //
/////////////
apply plugin: 'eclipse'
eclipse {
	project {
		natures 'org.eclipse.pde.PluginNature'
		natures 'org.eclipse.jdt.core.javanature'

		buildCommand 'org.eclipse.jdt.core.javabuilder'
		buildCommand 'org.eclipse.pde.ManifestBuilder'
		buildCommand 'org.eclipse.pde.SchemaBuilder'
	}
	classpath {
		downloadSources true
		downloadJavadoc true
	}
	jdt {
		sourceCompatibility VER_JAVA
		targetCompatibility VER_JAVA
	}
}
// always create fresh projects
tasks.eclipse.dependsOn(cleanEclipse)

////////////
// FORMAT //
////////////
spotless {
	java {
		licenseHeaderFile	rootProject.file('gradle/spotless.license.java')		// License header file
		eclipseFormatFile	rootProject.file('gradle/spotless.eclipseformat.xml')	// XML file dumped out by the Eclipse formatter
		importOrderFile		rootProject.file('gradle/spotless.importorder')			// An import ordering file, exported from Eclipse
		removeUnusedImports()
	}
	format 'misc', {
		target '.gitignore', '*.gradle', '*.md', '.ci/*.sh'
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
	freshmark {
		target '*.md'
		propertiesFile('gradle.properties')
		properties {
			it.put('group', 'com.diffplug.durian')
		}
	}
}

//////////////
// FINDBUGS //
//////////////
apply plugin: 'findbugs'
findbugs {
	toolVersion = VER_FINDBUGS
	sourceSets = [sourceSets.main]	// don't check the test code
	ignoreFailures = false 	// bug free or it doesn't ship!
	reportsDir = file('build/findbugs')
	effort = 'max'			// min|default|max
	reportLevel = 'low'		// low|medium|high (low = sensitive to even minor mistakes)
	omitVisitors = []		// bugs that we want to ignore
}
// HTML instead of XML
tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}
// we'll want the findbugs annotations (they don't have a 3.0.1 version)
dependencies {
	compile 'com.google.code.findbugs:annotations:3.0.0'
	compile 'com.google.code.findbugs:jsr305:3.0.0'
}

///////////
// MAVEN //
///////////
apply plugin: 'maven-publish'
group='com.diffplug.durian'

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

def verSnapshot = { it.endsWith('-SNAPSHOT') ? 'snapshot' : it }
// Where it's possible to name parameters and methods clearly enough
// that javadoc is not necessary, why make the code bigger?
//
// Thus, no javadoc warnings.
def makeLink = { url, text -> "<a href=\"${url}\" style=\"text-transform: none;\">${text}</a>" }
def javadocInfo = '<h2>' + makeLink("https://github.com/${org}/${name}", "${group}:${name}:${version}") +
' by ' + makeLink('http://www.diffplug.com', 'DiffPlug') + '</h2>'
javadoc {
	options.addStringOption('Xdoclint:none', '-quiet')
	options.header javadocInfo
	options.footer javadocInfo
	options.links('https://diffplug.github.io/durian/javadoc/durian-core/' + verSnapshot(VER_DURIAN) + '/')
	options.links('https://diffplug.github.io/durian/javadoc/durian-collect/' + verSnapshot(VER_DURIAN) + '/')
	options.links('https://diffplug.github.io/durian/javadoc/durian-concurrent/' + verSnapshot(VER_DURIAN) + '/')
	options.links('https://diffplug.github.io/durian-debug/javadoc/' + verSnapshot(VER_DURIAN_DEBUG) + '/')
	options.links('https://diffplug.github.io/durian-rx/javadoc/' + verSnapshot(VER_DURIAN_RX) + '/')
	options.links('https://docs.oracle.com/javase/8/docs/api/')
	options.links('http://reactivex.io/RxJava/javadoc/')
	options.links('http://help.eclipse.org/luna/nftopic/org.eclipse.platform.doc.isv/reference/api/')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

////////////////
// PUBLISHING //
////////////////
def isSnapshot = project.version.endsWith('-SNAPSHOT')
// pulls the credentials from either the environment variable or gradle.properties
def cred = {
	if (System.env[it] != null) {
		return System.env[it]
	} else if (project.hasProperty(it)) {
		return project[it]
	} else {
		return 'unknown_' + it
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom.withXml {
				// don't force our weird p2 ivy/p2 solution on our users
				asNode().dependencies.'*'.each() {
					if (it.groupId.text() == SWT_P2_GROUP) {
						it.parent().remove(it)
					}
				}
				// findbugs annotations should have scope "provided"
				asNode().dependencies.'*'.findAll() { it.groupId.text() == 'com.google.code.findbugs' }.each() { it.scope*.value = 'provided' }
				// add MavenCentral requirements to the POM
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name project.name
					description project.description
					url "https://github.com/${project.org}/${project.name}"
					scm {
						url "https://github.com/${project.org}/${project.name}"
						connection "scm:git:git://github.com/${project.org}/${project.name}"
						developerConnection "scm:git:ssh:git@github.com/${project.org}/${project.name}"
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'nedtwigg'
							name 'Ned Twigg'
							email 'ned.twigg@diffplug.com'
						}
					}
				}
			}
		}
	}
	if (isSnapshot) {
		// upload snapshots to oss.sonatype.org
		repositories { maven {
			url = 'https://oss.sonatype.org/content/repositories/snapshots'
			credentials {
				username = cred('nexus_user')
				password = cred('nexus_pass')
			}
		} }
	}
}

if (!isSnapshot) {
	// upload releases to bintray and then mavenCentral
	bintray {
		user = cred('bintray_user')
		key = cred('bintray_pass')
		publications = ['mavenJava']
		publish = true
		pkg {
			repo = 'opensource'
			name = project.name
			userOrg = project.org
			version {
				name = project.version
				mavenCentralSync {
					user = cred('nexus_user')
					password = cred('nexus_pass')
				}
			}
		}
	}

	publish.dependsOn(bintrayUpload)
	bintrayUpload.dependsOn(['generatePomFileForMavenJavaPublication', jar, sourcesJar, javadocJar])
}

//////////////////
// GITHUB PAGES //
//////////////////
githubPages {
	repoUri = "https://github.com/${project.org}/${project.name}"
	deleteExistingFiles = false
	pages {
		from javadoc.destinationDir
		into "javadoc/${verSnapshot(version)}"
	}
	credentials {
		username = cred('gh_token')
		password = ''
	}
}
tasks.prepareGhPages.dependsOn(":javadoc")
