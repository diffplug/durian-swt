apply plugin: 'com.diffplug.gradle.eclipse.mavencentral'
eclipseMavenCentral { release SWT_VERSION, {
	implementation 'org.eclipse.swt'
	implementation 'org.eclipse.jface'
	useNativesForRunningPlatform()
} }
dependencies {
	implementation project(':durian-swt.os')
	implementation "com.diffplug.durian:durian-rx:$VER_DURIAN_RX"
	implementation "io.reactivex.rxjava2:rxjava:$VER_RXJAVA"
	implementation "com.diffplug.durian:durian-core:$VER_DURIAN"
	implementation "com.diffplug.durian:durian-collect:$VER_DURIAN"
	implementation "com.diffplug.durian:durian-concurrent:$VER_DURIAN"
	compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
	compileOnly 'com.google.code.findbugs:annotations:3.0.1'

	testImplementation "junit:junit:$VER_JUNIT"
	testImplementation "org.assertj:assertj-core:$VER_ASSERTJ"
	testImplementation "com.diffplug.durian:durian-debug:$VER_DURIAN_DEBUG"
}

/////////////////////////
// INTERACTIVE TESTING //
/////////////////////////
// standard `gradlew test` will autoclose after 500ms
test {
	systemProperty 'com.diffplug.test.autoclose.milliseconds', '500'
	useJUnit {
		// there are some tests that can't pass without a user, so we'll exclude them
		excludeCategories 'com.diffplug.common.swt.InteractiveTest$FailsWithoutUser'
		// SWT tests don't work in gradle on OS X (https://github.com/ReadyTalk/swt-bling/issues/4)
		boolean isMac = System.getProperty("os.name").toLowerCase().contains("mac");
		if (isMac) {
			excludeCategories 'com.diffplug.common.swt.InteractiveTest'
		}
	}
	// log all test events
	testLogging {
		events "failed", "passed", "skipped", "standard_error", "standard_out", "started"
	}
}

// only run the interactive tests
task interactiveTest(type: Test) {
	systemProperty 'com.diffplug.test.autoclose.milliseconds', null
	useJUnit {
		includeCategories 'com.diffplug.common.swt.InteractiveTest'
	}
}
